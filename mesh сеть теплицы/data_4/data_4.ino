/*
 * Модуль для получения данных с датчиков влажности воздуха и земли, температуры
 * 
 * Распиновка для подключения:                          
 *            PIN 2 - пин данных для датчика dht11
 *            PIN 7 - пин питания датчика влажности земли
 *            PIN A0 - пин данных с датчика влажности земли
 *            
 * Идентификаторы и команды в сети:
 * 2 - включить реле увлажнителя воздуха
 * 3 - выключить реле увлажнителя воздуха
 * 4 - включить реле полива земли
 * 5 - выключить реле полива земли
 * 10 - форточка закрыта
 * 11 - форточка открыта на 30
 * 12 - форточка открыта на 45
 * 13 - форточка открыта на 90
 * 14 - форточка открыта на 110
 * от 50 до 200 - температура воздуха
 * от 200 до 400 - влажность воздуха
 * от 1000 до 3000 - влажность земли аналоговое значение
 * от 3000 до 5000 - уровень воды
 * 
 */
#include "DHT.h"                      // Подключаем библиотеку для работы с датчиком dht11 (dht sensor lib)
#include <Gyver433.h>                 // Подключаем библиотеку для работы с радиомодулем 433 МГц

#define DHTPIN 2                      //  Номер пина для подключения датчика температуры и влажности
#define SENSORPIN_POWER 7             //  Номер пина для подключения датчика влажности почвы - питание
#define SENSORPIN_A A0                //  Номер пина для подключения датчика влажности почвы - аналоговое значение

DHT dht(DHTPIN, DHT11);               //  Обьявляем обьект для работы с датчиком температуры и влажности
Gyver433_TX<3> tx;                    //  Обьявляем обьект для работы с радиомодулем - указываем пин подключения
 
 // Переменные -----------------------------------
 int temp = 0;       // Переменная значения температуры воздуха
 int air_h = 0;      // Переменная значения влажности воздуха
 int earth_h = 0;    // Переменная аналогового значения влажности земли
 bool earth = false; // Переменная логического значения влажности земли

 // Инициализация  -------------------------
void setup()
{
    pinMode(SENSORPIN_POWER, OUTPUT);  // Настройка пина питания - на выход
    digitalWrite(SENSORPIN_POWER, LOW);// При включении низкий уровень - выключен          
    Serial.begin(9600);         // Включаем серийный порт для отладки   
    dht.begin();                // Включаем датчик температуры и влажности
}

void loop() 
{                         
     digitalWrite(SENSORPIN_POWER, HIGH);// Включаем питание датчика влажности почвы
     delay(50);                          // Ждем для стабилизации питания
     earth_h = analogRead(SENSORPIN_A) + 1000;  // Читаем аналоговое значение от датчика влажности почвы и преобразуем до нужного значения    
     digitalWrite(SENSORPIN_POWER, LOW); // Выключаем питание датчика влажности почвы
        
     air_h = dht.readHumidity() + 300;   // Измеряем влажность и преобразуем до нужного значения
     temp = dht.readTemperature() + 100; // Измеряем температуру и преобразуем до нужного значения

     char data[4];// Массив буфер фиксированного размера для отправки
     sprintf(data, "%d", temp);// Преобразуем температуру воздуха
     tx.sendData(data);// Отправляем температуру воздух
     delay(100);
     sprintf(data, "%d", air_h);// Преобразуем влажность воздуха
     tx.sendData(data);// Отправляем влажность воздуха
     delay(100);
     sprintf(data, "%d", earth_h);// Преобразуем влажность земли
     tx.sendData(data);// Отправляем влажность земли
     
     delay(5000);                  // Задержки перед опросом датчиков 
}
